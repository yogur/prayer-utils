plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.jreleaser' version '1.19.0'
}

group = 'io.github.yogur'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation "net.e175.klaus:solarpositioning:2.0.6"

    testImplementation libs.junit.jupiter
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.19.2'
    testImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.19.2'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.github.yogur'
            artifactId = 'prayer-utils'
            
            from components.java

            pom {
                name = 'prayer-utils'
                description = 'High-precision Java library for calculating Islamic prayer times.'
                url = 'https://github.com/yogur/prayer-utils'
                licenses {
                    license {
                        name = 'Unlicense'
                        url = 'https://unlicense.org/'
                    }
                }
                developers {
                    developer {
                        id = 'yogur'
                        name = 'Abdul Rahman Al-Kibbe'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:yogur/prayer-utils.git'
                    developerConnection = 'scm:git:git@github.com:yogur/prayer-utils.git'
                    url = 'https://github.com/yogur/prayer-utils'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    project {
        name = 'prayer-utils'
        description = 'High-precision Java library for calculating Islamic prayer times.'
        website = 'https://github.com/yogur/prayer-utils'
        authors = ['Abdul Rahman Al-Kibbe']
        license = 'Unlicense'
        links {
            homepage = 'https://github.com/yogur/prayer-utils'
            documentation = 'https://github.com/yogur/prayer-utils#readme'
            license = 'https://unlicense.org/'
        }
    }
    release {
        github {
            repoOwner = 'yogur'
            name = 'prayer-utils'
            overwrite = true
        }
    }
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}
